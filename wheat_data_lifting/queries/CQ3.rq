prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
prefix rdfs:    <http://www.w3.org/2000/01/rdf-schema#> 
prefix xsd:     <http://www.w3.org/2001/XMLSchema#> 
prefix schema:  <http://schema.org/> 
prefix owl:     <http://www.w3.org/2002/07/owl#> 
prefix skos:    <http://www.w3.org/2004/02/skos/core#> 
prefix oa:      <http://www.w3.org/ns/oa#> 
prefix ncbi:    <http://identifiers.org/taxonomy/> 
prefix dct:     <http://purl.org/dc/terms/> 
prefix frbr:    <http://purl.org/vocab/frbr/core#> 
prefix fabio:   <http://purl.org/spar/fabio/> 
prefix obo:     <http://purl.obolibrary.org/obo/> 
prefix bibo: <http://purl.org/ontology/bibo/> 
prefix d2kab:   <http://ns.inria.fr/d2kab/> 
prefix dc: <http://purl.org/dc/terms/> 

SELECT distinct ?variety ?paper WHERE {

  ?rel1 d2kab:hasVariety ?a1 ;
           d2kab:hasPhenotype ?a2 .
  
  ?a1 a oa:Annotation ;
             oa:hasTarget [ oa:hasSource ?source1 ];  
             oa:hasBody [ a d2kab:Variety ; skos:prefLabel ?variety ] .

  ?a2 a oa:Annotation ;
             oa:hasTarget  [ oa:hasSource ?source2 ] ;
             oa:hasBody [ skos:prefLabel "heat resistance"  ]  .

  ?source1 frbr:partOf+ ?paper.
  ?source2 frbr:partOf+ ?paper. 
  ?paper  a fabio:ResearchPaper .
}